plugins {
    id 'java-library'
    id 'java-gradle-plugin'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

description 'Present RPC compiler'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    implementation deps.wire.compiler
    implementation deps.wire.schema
    implementation 'info.picocli:picocli:3.9.5'
    implementation 'com.github.spullara.mustache.java:compiler:0.9.5'
}

jar {
    manifest {
        attributes 'Main-Class': 'present.rpc.RpcCompiler'
    }
}

gradlePlugin {
    plugins {
        rpcCompiler {
            id = 'co.present.rpc.compiler'
            implementationClass = 'present.rpc.RpcCompilerPlugin'
        }
    }
}

shadowJar {
    baseName = 'present-rpc-compiler'
    classifier = null
    version = null
    minimize()
}

build {
    dependsOn 'shadowJar'
}

// Workaround: This disables the broken publications that seem to be added by java-gradle-plugin.
afterEvaluate {
    tasks.withType(PublishToMavenRepository) { task ->
        if (task.publication.name != 'mavenJava') {
            task.enabled = false
            task.group = null
        }
    }
}